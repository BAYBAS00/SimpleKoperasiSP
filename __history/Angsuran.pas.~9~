unit Angsuran;

interface

uses
  Winapi.Windows, Winapi.Messages, System.SysUtils, System.Variants, System.Classes, Vcl.Graphics,
  Vcl.Controls, Vcl.Forms, Vcl.Dialogs, Vcl.StdCtrls, Data.DB, Vcl.Grids,
  Vcl.DBGrids, Vcl.ComCtrls, MemDS, DBAccess, MyAccess, Vcl.Buttons,
  Vcl.ExtCtrls;

type
  TFAngsuran = class(TForm)
    Label1: TLabel;
    edKodeAngsuran: TEdit;
    Label2: TLabel;
    cbKodePinjaman: TComboBox;
    Label3: TLabel;
    edNamaAnggota: TEdit;
    Label4: TLabel;
    dtTanggal: TDateTimePicker;
    Label5: TLabel;
    edJumlah: TEdit;
    DBGrid1: TDBGrid;
    Panel1: TPanel;
    Splitter1: TSplitter;
    Splitter2: TSplitter;
    btnKeluar: TBitBtn;
    btnSimpan: TBitBtn;
    QAngsuran: TMyQuery;
    DSAngsuran: TDataSource;
    QAngsurankode_angsuran: TStringField;
    QAngsurankode_pinjaman: TStringField;
    QAngsurankode_anggota: TStringField;
    QAngsurannama: TStringField;
    QAngsurantanggal: TDateField;
    QAngsuranjumlah_angsuran: TFloatField;
    btnEdit: TBitBtn;
    btnHapus: TBitBtn;
    btnBatal: TBitBtn;
    btnRefresh: TBitBtn;
    procedure FormShow(Sender: TObject);
    procedure cbKodePinjamanChange(Sender: TObject);
    procedure btnSimpanClick(Sender: TObject);
    procedure btnRefreshClick(Sender: TObject);
    procedure btnBatalClick(Sender: TObject);
    procedure btnEditClick(Sender: TObject);
    procedure btnHapusClick(Sender: TObject);
  private
    { Private declarations }
    procedure LoadPinjaman;
    procedure GenerateKodeAngsuran;
    procedure CekStatusLunas(idPinjaman: Integer);
    procedure KosongkanField;
  public
    { Public declarations }
    isEditMode: Boolean;
  end;

var
  FAngsuran: TFAngsuran;

implementation

{$R *.dfm}

uses Pinjaman, UDM;

procedure TFAngsuran.KosongkanField;
begin
  cbKodePinjaman.Clear;
  edNamaAnggota.Clear;
  edJumlah.Clear;
  dtTanggal.Date := Date;
end;

procedure TFAngsuran.LoadPinjaman;
begin
  cbKodePinjaman.Clear;
  FUDM.Qtemp.SQL.Text := 'SELECT kode_pinjaman FROM pinjaman ORDER BY kode_pinjaman';
  FUDM.Qtemp.Open;
  while not FUDM.Qtemp.Eof do
  begin
    cbKodePinjaman.Items.Add(FUDM.Qtemp.FieldByName('kode_pinjaman').AsString);
    FUDM.Qtemp.Next;
  end;
  FUDM.Qtemp.Close;
end;

procedure TFAngsuran.FormShow(Sender: TObject);
begin
  LoadPinjaman;
  GenerateKodeAngsuran;
  dtTanggal.Date := Date;
  QAngsuran.Close;
  QAngsuran.Open;
end;

procedure TFAngsuran.GenerateKodeAngsuran;
var
  angka: Integer;
begin
  FUDM.Qtemp.SQL.Text := 'SELECT kode_angsuran FROM angsuran ORDER BY id_angsuran DESC LIMIT 1';
  FUDM.Qtemp.Open;
  if not FUDM.Qtemp.IsEmpty then
    angka := StrToIntDef(Copy(FUDM.Qtemp.FieldByName('kode_angsuran').AsString, 4, 3), 0) + 1
  else
    angka := 1;
  edKodeAngsuran.Text := Format('AGS%.3d', [angka]);
  FUDM.Qtemp.Close;
end;

procedure TFAngsuran.btnBatalClick(Sender: TObject);
begin
  KosongkanField;
  isEditMode := False;
  GenerateKodeAngsuran;
end;

procedure TFAngsuran.btnEditClick(Sender: TObject);
begin
  if QAngsuran.IsEmpty then
  begin
    ShowMessage('Pilih data yang ingin diedit.');
    Exit;
  end;

  isEditMode := True; // Aktifkan mode edit

  edKodeAngsuran.Text := QAngsuran.FieldByName('kode_angsuran').AsString;
  LoadPinjaman;
  cbKodePinjaman.ItemIndex := cbKodePinjaman.Items.IndexOf(QAngsuran.FieldByName('kode_pinjaman').AsString);
  cbKodePinjamanChange(nil); // untuk isi nama anggota
  dtTanggal.Date := QAngsuran.FieldByName('tanggal').AsDateTime;
  edJumlah.Text := FloatToStr(QAngsuran.FieldByName('jumlah_angsuran').AsFloat);
end;

procedure TFAngsuran.btnHapusClick(Sender: TObject);
var
  kodePinjaman, statusLunas: string;
begin
  if QAngsuran.IsEmpty then
  begin
    ShowMessage('Pilih data angsuran yang ingin dihapus.');
    Exit;
  end;

  // Ambil kode_pinjaman dari record angsuran yang dipilih
  kodePinjaman := QAngsuran.FieldByName('kode_pinjaman').AsString;

  // Cek status lunas dari tabel pinjaman
  FUDM.Qtemp.SQL.Text := 'SELECT status_lunas FROM pinjaman WHERE kode_pinjaman = :kode';
  FUDM.Qtemp.ParamByName('kode').AsString := kodePinjaman;
  FUDM.Qtemp.Open;
  statusLunas := FUDM.Qtemp.FieldByName('status_lunas').AsString;
  FUDM.Qtemp.Close;

  if statusLunas = 'Lunas' then
  begin
    ShowMessage('Data angsuran tidak dapat dihapus karena pinjaman sudah lunas.');
    Exit;
  end;

  // Konfirmasi dan hapus jika tidak lunas
  if MessageDlg('Yakin ingin menghapus data angsuran ini?', mtConfirmation, [mbYes, mbNo], 0) = mrYes then
  begin
    FUDM.Qtemp.SQL.Text := 'DELETE FROM angsuran WHERE kode_angsuran = :kode';
    FUDM.Qtemp.ParamByName('kode').AsString := QAngsuran.FieldByName('kode_angsuran').AsString;
    FUDM.Qtemp.ExecSQL;

    ShowMessage('Data angsuran berhasil dihapus.');

    // Refresh data dan reset form
    btnRefreshClick(nil);
    KosongkanField;
    GenerateKodeAngsuran;
  end;
end;

procedure TFAngsuran.btnRefreshClick(Sender: TObject);
begin
  QAngsuran.Close;
  QAngsuran.Open;
end;

procedure TFAngsuran.btnSimpanClick(Sender: TObject);
var
  idPinjaman, tenor: Integer;
begin
  if isEditMode then
  begin
    // Mode Edit: Update data
    FUDM.Qtemp.SQL.Text :=
      'UPDATE angsuran SET kode_pinjaman = :pinjaman, tanggal = :tgl, jumlah_angsuran = :jumlah ' +
      'WHERE kode_angsuran = :kode';
  end
  else
  begin
    // Mode Tambah: Insert data
    FUDM.Qtemp.SQL.Text :=
      'INSERT INTO angsuran (kode_angsuran, kode_pinjaman, tanggal, jumlah_angsuran) ' +
      'VALUES (:kode, :pinjaman, :tgl, :jumlah)';
  end;

  FUDM.Qtemp.ParamByName('kode').AsString := edKodeAngsuran.Text;
  FUDM.Qtemp.ParamByName('pinjaman').AsString := cbKodePinjaman.Text;
  FUDM.Qtemp.ParamByName('tgl').AsDate := dtTanggal.Date;
  FUDM.Qtemp.ParamByName('jumlah').AsFloat := StrToFloatDef(edJumlah.Text, 0);
  FUDM.Qtemp.ExecSQL;

  // Ambil ID pinjaman
  FUDM.Qtemp.SQL.Text := 'SELECT id_pinjaman, tenor FROM pinjaman WHERE kode_pinjaman = :kode';
  FUDM.Qtemp.ParamByName('kode').AsString := cbKodePinjaman.Text;
  FUDM.Qtemp.Open;
  idPinjaman := FUDM.Qtemp.FieldByName('id_pinjaman').AsInteger;
  tenor := FUDM.Qtemp.FieldByName('tenor').AsInteger;
  FUDM.Qtemp.Close;

  CekStatusLunas(idPinjaman);

  if isEditMode then
    ShowMessage('Data angsuran berhasil diperbarui.')
  else
    ShowMessage('Data angsuran berhasil disimpan.');

  // Reset form
  isEditMode := False;
  KosongkanField;
  GenerateKodeAngsuran;
  btnRefreshClick(nil);
end;

procedure TFAngsuran.cbKodePinjamanChange(Sender: TObject);
begin
  if cbKodePinjaman.Text = '' then Exit;

  FUDM.Qtemp.SQL.Text :=
    'SELECT a.nama FROM pinjaman p JOIN anggota a ON p.kode_anggota = a.kode_anggota ' +
    'WHERE p.kode_pinjaman = :kode';
  FUDM.Qtemp.ParamByName('kode').AsString := cbKodePinjaman.Text;
  FUDM.Qtemp.Open;
  edNamaAnggota.Text := FUDM.Qtemp.FieldByName('nama').AsString;
  FUDM.Qtemp.Close;
end;

procedure TFAngsuran.CekStatusLunas(idPinjaman: Integer);
var
  total, cicilan: Double;
  tenor: Integer;
begin
  // Ambil total angsuran
  FUDM.Qtemp.SQL.Text :=
    'SELECT SUM(jumlah_angsuran) AS total FROM angsuran WHERE kode_pinjaman = ' +
    '(SELECT kode_pinjaman FROM pinjaman WHERE id_pinjaman = :id)';
  FUDM.Qtemp.ParamByName('id').AsInteger := idPinjaman;
  FUDM.Qtemp.Open;
  total := FUDM.Qtemp.FieldByName('total').AsFloat;
  FUDM.Qtemp.Close;

  // Ambil tenor dan cicilan
  FUDM.Qtemp.SQL.Text := 'SELECT tenor, cicilan FROM pinjaman WHERE id_pinjaman = :id';
  FUDM.Qtemp.ParamByName('id').AsInteger := idPinjaman;
  FUDM.Qtemp.Open;
  tenor := FUDM.Qtemp.FieldByName('tenor').AsInteger;
  cicilan := FUDM.Qtemp.FieldByName('cicilan').AsFloat;
  FUDM.Qtemp.Close;

  // Bandingkan total angsuran dengan total cicilan
  if total >= tenor * cicilan then
  begin
    FUDM.Qtemp.SQL.Text := 'UPDATE pinjaman SET status_lunas = "Lunas" WHERE id_pinjaman = :id';
    FUDM.Qtemp.ParamByName('id').AsInteger := idPinjaman;
    FUDM.Qtemp.ExecSQL;
  end;
end;


end.
